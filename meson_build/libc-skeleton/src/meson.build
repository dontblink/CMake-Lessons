#Libc files

gdtoa_files = files(
	'gdtoa/src/dmisc.c',
	'gdtoa/src/dtoa.c',
	'gdtoa/src/g__fmt.c',
	'gdtoa/src/g_ddfmt.c',
	'gdtoa/src/g_dfmt.c',
	'gdtoa/src/g_ffmt.c',
	'gdtoa/src/g_Qfmt.c',
	'gdtoa/src/g_xfmt.c',
	'gdtoa/src/g_xLfmt.c',
	'gdtoa/src/gdtoa.c',
	'gdtoa/src/gethex.c',
	'gdtoa/src/gmisc.c',
	'gdtoa/src/hd_init.c',
	'gdtoa/src/hexnan.c',
	'gdtoa/src/misc.c',
	'gdtoa/src/smisc.c',
	'gdtoa/src/strtod.c',
	'gdtoa/src/strtodg.c',
	'gdtoa/src/strtodI.c',
	'gdtoa/src/strtof.c',
	'gdtoa/src/strtoId.c',
	'gdtoa/src/strtoIdd.c',
	'gdtoa/src/strtoIf.c',
	'gdtoa/src/strtoIg.c',
	'gdtoa/src/strtoIQ.c',
	'gdtoa/src/strtoIx.c',
	'gdtoa/src/strtoIxL.c',
	'gdtoa/src/strtopd.c',
	'gdtoa/src/strtopdd.c',
	'gdtoa/src/strtopf.c',
	'gdtoa/src/strtopQ.c',
	'gdtoa/src/strtopx.c',
	'gdtoa/src/strtopxL.c',
	'gdtoa/src/strtord.c',
	'gdtoa/src/strtordd.c',
	'gdtoa/src/strtorf.c',
	'gdtoa/src/strtorQ.c',
	'gdtoa/src/strtorx.c',
	'gdtoa/src/strtorxL.c',
	'gdtoa/src/sum.c',
	'gdtoa/src/ulp.c'
)

# libgdtoa = static_library('gdtoa',
# 	gdtoa_files,
# 	include_directories: gdtoa_includes,
# 	c_args: gdtoa_compiler_flags,
# )

assert_files = files('assert/assert.c')

crt_files = files(
	'crt/_Exit.c',
	'crt/abort.c',
	'crt/at_exit.c',
	'crt/at_quick_exit.c',
	'crt/crt.c',
	'crt/cxa_atexit.c',
	'crt/exit.c',
	'crt/quick_exit.c',
)

ctype_files = files(
	'ctype/isalnum.c',
	'ctype/isalpha.c',
	'ctype/isascii.c',
	'ctype/isblank.c',
	'ctype/iscntrl.c',
	'ctype/isdigit.c',
	'ctype/isgraph.c',
	'ctype/islower.c',
	'ctype/isprint.c',
	'ctype/ispunct.c',
	'ctype/isspace.c',
	'ctype/isupper.c',
	'ctype/isxdigit.c',
	'ctype/toascii.c',
	'ctype/tolower.c',
	'ctype/toupper.c'
)

stdlib_files = files(
	'stdlib/abs.c',
	'stdlib/atof.c',
	'stdlib/atoi.c',
	'stdlib/atol.c',
	'stdlib/atoll.c',
	'stdlib/bsearch.c',
	'stdlib/calloc.c',
	'stdlib/div.c',
	'stdlib/heapsort_r.c',
	'stdlib/heapsort.c',
	'stdlib/imaxabs.c',
	'stdlib/imaxdiv.c',
	'stdlib/labs.c',
	'stdlib/ldiv.c',
	'stdlib/llabs.c',
	'stdlib/lldiv.c',
	'stdlib/qsort_r.c',
	'stdlib/qsort.c',
	'stdlib/rand.c',
	'stdlib/realloc.c',
	'stdlib/strtol.c',
	'stdlib/strtold.c',
	'stdlib/strtoul.c',
	'stdlib/strtoll.c',
	'stdlib/strtoull.c',
)

wchar_files = files(
	'wchar/iswalnum.c',
	'wchar/iswalpha.c',
	'wchar/iswblank.c',
	'wchar/iswcntrl.c',
	'wchar/iswalnum.c',
	'wchar/iswalpha.c',
	'wchar/iswblank.c',
	'wchar/iswcntrl.c',
	'wchar/iswctype.c',
	'wchar/iswdigit.c',
	'wchar/iswgraph.c',
	'wchar/iswlower.c',
	'wchar/iswprint.c',
	'wchar/iswpunct.c',
	'wchar/iswspace.c',
	'wchar/iswupper.c',
	'wchar/iswxdigit.c',
	'wchar/towccase.c',
	'wchar/towctrans.c',
	'wchar/towlower.c',
	'wchar/towupper.c',
	'wchar/wcswidth.c',
	'wchar/wctrans.c',
	'wchar/wctype.c',
	'wchar/wcwidth.c',
)


locale_files = files(
	'locale/langinfo.c',

)

math_files = files(
	'math/fabs.c',
	'math/fabsf.c',
)

stdio_files = files(
	'stdio/asprintf.c',
	'stdio/putchar.c',
	'stdio/puts.c',
	'stdio/vasprintf.c',
)

support_files = files(
	'support/fls.c',
	'support/flsl.c',
	'support/flsll.c',
)

time_files = files(
	'time/asctime.c',
	'time/asctime_r.c',
)

string_files = files(
	'string/memcmp.c',
	'string/memcpy.c',
	'string/memmem.c',
	'string/memmove.c',
	'string/memchr.c',
	'string/memrchr.c',
	'string/memset.c',
	'string/strcat.c',
	'string/strchr.c',
	'string/strchrnul.c',
	'string/strcmp.c',
	'string/strcoll.c',
	'string/strcpy.c',
	'string/strcspn.c',
	'string/strdup.c',
	'string/strerror.c',
	'string/strerror_r.c',
	'string/strlen.c',
	'string/strncat.c',
	'string/strncmp.c',
	'string/strncpy.c',
	'string/strndup.c',
	'string/strnlen.c',
	'string/strnstr.c',
	'string/strpbrk.c',
	'string/strrchr.c',
	'string/strspn.c',
	'string/strstr.c',
	'string/strtok.c',
	'string/strxfrm.c',
)

libc_common_files = [
	assert_files,
	ctype_files,
	gdtoa_files,
	locale_files,
	math_files,
	string_files,
	stdio_files,
	stdlib_files,
	support_files,
	time_files,
	wchar_files,
]

# These files are used to compile libc for a build machine / your personal computer
libc_native_files = files(
	'stdio/putchar_native.c',
)

# These files are used when cross-compiling libc for another platform
libc_standalone_files = [
	crt_files,
]

#Libc Includes ##################################
gdtoa_includes = include_directories('gdtoa/include')
gdtoa_sys_includes = include_directories('gdtoa/include', is_system: true)

# .. is used here because we are in the src/ directory, and include/ is one directory up
libc_includes = [include_directories('../include'), gdtoa_includes]
libc_sys_includes = [include_directories('../include',  is_system: true), gdtoa_sys_includes]

#Libc compiler flags ##############################

gdtoa_compiler_flags = [
	'-DNO_ERRNO',
	'-DIFNAN_CHECK',
	'-DGDTOA_NO_ASSERT',
	'-DNO_FENV_H'
]

libc_compile_flags = gdtoa_compiler_flags

if disable_unimplemented_apis
	libc_compile_flags += '-DDISABLE_UNIMPLEMENTED_LIBC_APIS' 	# Note: this needs to be set for both libc compilation + dependency compile_args
endif

if enable_gnu_src
	libc_compile_flags += '-D_GNU_SOURCE' # Note: This needs to be set for both libc compilation + dependency compile_args
endif

#Libc native/host compiler flags ########################
libc_host_compile_flags = '-nostdinc' # Don't include standard include paths - we don't want system headers,	
libc_native_compile_flags = '-nostdinc' # Don't include standard include paths - we don't want system headers,	

if disable_builtins
	libc_host_compile_flags += '-fno-builtin'
	libc_native_compile_flags += '-fno-builtin'
endif

if disable_stack_protection
	libc_host_compile_flags += '-fno-stack-protector'
	libc_native_compile_flags += '-fno-stack-protector'
endif

#Libc Linker Flags ###############################
# Note - these linker flags aren't needed when compiling the libc project itself, they're used by downstream modules
# to ensure that they actually use this libc rather than using the compiler-provided version
libc_native_link_flags = [
	'-nolibc',	
]

libc_host_link_flags = [
	'-nolibc',
	'-nostartfiles',	
]

if native_c_compiler_id == 'clang'
	# Clang does nothing with -nolibc. We are forced to upgrade to -nostdlib
	libc_native_link_flags += '-nostdlib'

	# We can't do this for Linux, because we need the GCC startup files (for now)
	if build_machine.system() != 'linux'
		libc_hosted_native_link_flags += '-nostdlib'
	endif
endif

libc_hosted_native_link_flags = libc_native_link_flags

# Filter flags and only use the ones that are actually supported
# We don't use the linker API due to false detections of "NO" values b/c of valid linker errors
libc_host_compile_flags = host_c_compiler.get_supported_arguments(libc_host_compile_flags)
libc_native_compile_flags = native_c_compiler.get_supported_arguments(libc_native_compile_flags)
libc_native_link_flags = native_c_compiler.get_supported_arguments(libc_native_link_flags)
libc_host_link_flags = host_c_compiler.get_supported_arguments(libc_host_link_flags)
libc_hosted_native_link_flags = native_c_compiler.get_supported_arguments(libc_hosted_native_link_flags)

# these need to be added post filtering, as get_supported_arguments does not suupport `-l` flag
if(build_machine.system() == 'darwin')
	# The following flags are required to link and run an app on OSX
	libc_hosted_native_link_flags += ['-lSystem']
elif(build_machine.system() == 'linux')
	#The following flags are required to link and run an app on Linux (with native hosted libc)
	libc_hosted_native_link_flags += ['-lc']
else
	warning('Operating system not handled for hosted native linker flags.')
endif

#Libc static libaries ############################
libc_native = static_library(
	'c_native',
	libc_common_files + libc_native_files,
	c_args: libc_compile_flags + libc_native_compile_flags,
	include_directories: [libc_includes, build_machine_arch_specific_includes, gdtoa_includes],
	dependencies: [printf_files_dep, openlibm_dep],
	native: true,
)

libc = static_library(
	'c',
	libc_common_files + libc_standalone_files,
	c_args: libc_compile_flags + libc_host_compile_flags,
	include_directories: [libc_includes, host_machine_arch_specific_includes, gdtoa_includes],
	dependencies: [printf_files_dep, openlibm_dep],
	native: false,
)


#libc dependencies ################################
libc_native_dep = declare_dependency(
	include_directories: [printf_includes, openlibm_includes, libc_sys_includes, gdtoa_sys_includes, build_machine_arch_specific_sys_includes],
	compile_args: libc_compile_flags +libc_native_compile_flags,
	link_args: libc_native_link_flags,
	link_with: libc_native,
)

libc_dep = declare_dependency(
	include_directories: [printf_includes, openlibm_includes, libc_sys_includes, gdtoa_sys_includes, host_machine_arch_specific_sys_includes],
	compile_args: libc_compile_flags + libc_host_compile_flags,
	link_args: libc_host_link_flags,
	link_with: libc,
)

libc_hosted_native_dep = declare_dependency(
	include_directories: [
		libc_sys_includes,
		gdtoa_sys_includes,
		build_machine_arch_specific_sys_includes,
		openlibm_includes,
		printf_includes
	],
	compile_args: libc_compile_flags + libc_host_compile_flags,
	link_args: libc_hosted_native_link_flags,
	link_with: libc_native,
)
